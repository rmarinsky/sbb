plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'ua.marinsky'
version '0.2'

repositories {
    mavenCentral()
}

ext {
    junitVer = '5.7.2'
}

dependencies {
    testImplementation(
            "org.junit.jupiter:junit-jupiter-api:$junitVer",
            "org.junit.jupiter:junit-jupiter-engine:$junitVer",
            "org.assertj:assertj-core:3.20.2"
    )
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

publishing {
    repositories {
        maven {
            name = 'StringBuilderBuilder'
            description = "String Builder that doesn't sucks"
            url = uri('https://maven.pkg.github.com/rmarinsky/stringBuilderBuilder')

            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
